plugins {
    id 'java'
    id 'idea'
}

group 'com.github.kostrovik'
version '1.0-SNAPSHOT'
def moduleName = 'useful.utils'

sourceCompatibility = 10
targetCompatibility = 10
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testCompile("org.junit.jupiter:junit-jupiter-api:5.2.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.2.0")
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

test {
    useJUnitPlatform()
}

idea {
    module {
        inheritOutputDirs = true
    }
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes(
                'Automatic-Module-Name': moduleName,
        )
    }
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

task addDependencies(type: Copy) {
    from configurations.runtime
    into "build/libs"
}

task copyToLib(type: Copy) {
    from 'build/libs'
    into "./modules/"

    dependsOn addDependencies
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}